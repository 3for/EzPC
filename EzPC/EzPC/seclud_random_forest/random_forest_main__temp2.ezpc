(*
Authors: Nishant Kumar.
Copyright:
Copyright (c) 2018 Microsoft Research
Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:
The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.
THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*)
(*************** gt ***************)
def bool_bl signedgtbl(int64_bl x, int64_bl y){
 uint64_bl ux = x;
 uint64_bl uy = y;
 uint64_bl signBitX = (x & (1L<<63L));
 uint64_bl signBitY = (y & (1L<<63L));
 return ((signBitX ^ signBitY) > 0uL) ? ((signBitX > 0uL) ? false : true) : (ux > uy);
}
(*************** arshift ***************)
(*
* Note : Since the 2nd arg is public, another function with signed int as 2nd arg is not needed.
*)
def int64_bl signedarshiftbl(int64_bl x, uint64_pl y){
 uint64_bl ux = x;
 uint64_bl signBitX = (x & (1L<<63L));
 return ((signBitX > 0uL) ? (0uL - ((0uL - ux) >> y)) : (ux >> y));
}
(*************** lt ***************)
def bool_bl unsignedltbl(uint64_bl x, uint64_bl y){
 return (y > x);
}
def bool_bl signedltbl(int64_bl x, int64_bl y){
 return (y > x);
}
(*************** leq ***************)
def bool_bl unsignedleqbl(uint64_bl x, uint64_bl y){
 return !(x > y);
}
def bool_bl signedleqbl(int64_bl x, int64_bl y){
 return !(x > y)
}
(*************** geq ***************)
def bool_bl unsignedgeqbl(uint64_bl x, uint64_bl y){
 return !(y > x);
}
def bool_bl signedgeqbl(int64_bl x, int64_bl y){
 return !(y > x);
}
(*************** equals ***************)
def bool_bl unsignedequalsbl(uint64_bl x, uint64_bl y){
 return (!(x < y)) && (!(y < x));
}
def bool_bl signedequalsbl(int64_bl x, int64_bl y){
 return (!(x < y)) && (!(y < x));
}
(*************** div ***************)
(*
* Long division implemented in ezpc
* If getQuotient is true, return quotient
* else return remainder
*)
def uint64_bl longDivision(uint64_bl x, uint64_bl y, bool_pl getQuotient){
 (* Long division *)
 uint64_bl q = 0uL;
 uint64_bl divisor = 0uL;
 bool_bl cond = false;
 for iter=[0:64]
 {
  uint64_pl i = 63L - iter;
  divisor = divisor << 1uL;
  divisor = divisor +_bl ((x & (1uL << i)) >>> i);
  cond = (divisor >= y);
  divisor = (cond) ? (divisor -_bl y) : divisor;
  q = q << 1uL;
  q = (cond) ? q + 1uL : q;
 };
 (*At the end of this loop, q is the quotient and divisor contains the remainder*)
 return (getQuotient ? q : divisor);
}
def uint64_bl unsigneddivbl(uint64_bl x, uint64_bl y){
 return longDivision(x, y, true);
}
def uint64_bl unsigneddival(uint64_al x, uint64_al y){
 uint64_bl bx = x;
 uint64_bl by = y;
 return bx/by;
}
def int64_bl signeddivbl(int64_bl x, int64_bl y){
 bool_bl isXNeg = (x < 0L);
 bool_bl isYNeg = (y < 0L);
 uint64_bl ux = isXNeg ? (0L - x) : x;
 uint64_bl uy = isYNeg ? (0L - y) : y;
 uint64_bl ures = ux/uy;
 bool_bl isResNeg = (isXNeg @ isYNeg);
 return (isResNeg ? (0uL - ures) : ures);
}
def int64_bl signeddival(int64_al x, int64_al y){
 int64_bl bx = x;
 int64_bl by = y;
 return bx/by;
}
(*************** mod ***************)
(*
* NOTE : Given any signed/unsigned ints x and y, following is always true :
* x = (x/y) * y + (x%y)
*)
def uint64_bl unsignedmodbl(uint64_bl x, uint64_bl y){
 return longDivision(x, y, false);
}
def uint64_bl unsignedmodal(uint64_al x, uint64_al y){
 uint64_bl bx = x;
 uint64_bl by = y;
 return bx%by;
}
def int64_bl signedmodbl(int64_bl x, int64_bl y){
 bool_bl isXNeg = (x < 0L);
 bool_bl isYNeg = (y < 0L);
 uint64_bl ux = isXNeg ? (0L - x) : x;
 uint64_bl uy = isYNeg ? (0L - y) : y;
 uint64_bl urem = ux%uy;
 return (isXNeg ? (0uL - urem) : urem);
}
def int64_bl signedmodal(int64_al x, int64_al y){
 int64_bl bx = x;
 int64_bl by = y;
 return bx%by;
}
(* Random Forest implementation for inference on California Housing Dataset -- Regression *)
(* These values come from a python program that I used to train the Random Forest. Accuracy on testing set (20% of full dataset) = 83.04 *)
int32 noOfFeatures = 13;
(* This depth is the depth in python code + 1 *)
int32 maxDepth = 10;
int32 maxDepthInt = 10;
int32 noOfTrees = 10;
int32 noOfNodes = 1023;
(* To get secret shares of an element from array when the element's index is secret shared *)
def void accessElementOneD(int64_bl[noOfFeatures] arr, int64_bl idx, int64_bl[1] result)
{
 result[0] = 0L;
 int64_pl ctr = 0L;
 for i=[0:noOfFeatures]
 {
  result[0] = result[0] +_bl (idx == ctr?arr[i]:0L);
  ctr = ctr + 1L;
 };
}
def void accessElementTwoD(int64_bl[noOfTrees][noOfNodes] arr, int64_pl treeId, int64_al idx, int64_bl[1] result, int32_pl start, int32_pl end)
{
 result[0] = 0L;
 int64_pl ctr = start + 0L;
 int64_bl idxcpy = idx;
 for i=[start:end]
 {
  result[0] = result[0] +_bl (idxcpy == ctr?arr[treeId][i]:0L);
  ctr = ctr + 1L;
 };
}
def int32_pl pow(int32_pl base, int32_pl power)
{
 int32_pl result = 1;
 for i=[0:power]
 {
  result = result * base;
 };
 return result;
}
def void infer(int64_bl[noOfTrees][noOfNodes] modelFeatureChoice,
  int64_bl[noOfTrees][noOfNodes] modelThresholdValue,
  int64_bl[noOfFeatures] query,
  int64_bl[noOfTrees] result)
{
 int64_al currentIndex = 0L;
 int64_al leftOrRight = 0L;
 int64_bl[1] currentSharedFeatureChoice;
 int64_bl[1] currentSharedThreshold;
 int64_bl[1] currentSharedQueryValue;
 currentSharedFeatureChoice[0] = 0L;
 currentSharedThreshold[0] = 0L;
 currentSharedQueryValue[0] = 0L;
 int32_pl startIdx = 0;
 int32_pl endIdx = 0;
 for i=[0:noOfTrees]
 {
  currentIndex = 0L;
  (* Root level *)
  currentSharedFeatureChoice[0] = modelFeatureChoice[i][0];
  currentSharedThreshold[0] = modelThresholdValue[i][0];
  accessElementOneD(query, currentSharedFeatureChoice[0], currentSharedQueryValue);
  leftOrRight = (currentSharedThreshold[0] > currentSharedQueryValue[0])?1L:2L;
  for j=[1:maxDepthInt-1]
  {
   currentIndex = (currentIndex *_al 2L) +_al leftOrRight;
   startIdx = pow(2, j)-1;
   endIdx = pow(2, j+1)-1;
   accessElementTwoD(modelThresholdValue, i, currentIndex, currentSharedThreshold, startIdx, endIdx);
   accessElementTwoD(modelFeatureChoice, i, currentIndex, currentSharedFeatureChoice, startIdx, endIdx);
   accessElementOneD(query, currentSharedFeatureChoice[0], currentSharedQueryValue);
   leftOrRight = (currentSharedThreshold[0] > currentSharedQueryValue[0])?1L:2L;
  };
  (* Leaf level *)
  currentIndex = (currentIndex *_al 2L) +_al leftOrRight;
  startIdx = pow(2, maxDepthInt-1)-1;
  endIdx = pow(2, maxDepthInt)-1;
  accessElementTwoD(modelThresholdValue, i, currentIndex, currentSharedThreshold, startIdx, endIdx);
  result[i] = currentSharedThreshold[0];
 };
}
def void main()
{
 (* Taking inputs as boolean shares because comparison are performed *)
 int64_bl[noOfFeatures] inferenceQuery;
 int64_bl[noOfTrees][noOfNodes] modelFeatureChoice;
 (* Threshold value for leaves = inference output from that tree *)
 int64_bl[noOfTrees][noOfNodes] modelThresholdValue;
 int64_bl[noOfTrees] inferenceResult;
 int64_bl finalResult = 0L;
 (* One party inputs the inference query *)
 for i=[0:noOfFeatures]
 {
  input(CLIENT, temp, int64_bl);
  inferenceQuery[i] = temp;
 };
 (* Other party inputs the trained model in the form of the feature choice
 at each node as well as the threshold value of the feature at that node *)
 for i=[0:noOfTrees]
 {
  for j=[0:noOfNodes]
  {
   input(SERVER, temp, int64_bl);
   modelFeatureChoice[i][j] = temp;
  };
  for j=[0:noOfNodes]
  {
   input(SERVER, temp, int64_bl);
   modelThresholdValue[i][j] = temp;
  };
 };
 infer(modelFeatureChoice, modelThresholdValue, inferenceQuery, inferenceResult);
 (* Take mean of all the values in inference result *)
 for i=[0:noOfTrees]
 {
  finalResult = finalResult +_al inferenceResult[i];
 };
 (* finalResult = finalResult/(noOfTrees+0L); *)
 output(CLIENT, finalResult)
}
